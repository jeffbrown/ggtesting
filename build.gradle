buildscript {
    dependencies {
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0'
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'com.github.jruby-gradle.base' version '1.4.0'
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'org.asciidoctor.convert'

ext {
    deckjsVersion = 'master'
    asciidoctorBackendVersion = 'master'
    downloadDir = new File(buildDir,'download')
    templateDir = new File(downloadDir,'templates')
    deckjsDir   = new File(downloadDir,'deck.js')
}

repositories {
    jcenter()
}

dependencies {
    gems 'rubygems:haml:4.0.7'
}

task download {
    doLast {
        mkdir downloadDir
        vfs {
            cp "zip:https://github.com/asciidoctor/asciidoctor-deck.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-deck.js-${asciidoctorBackendVersion}/templates",
            templateDir, recursive:true, overwrite:true
            cp "zip:https://github.com/imakewebthings/deck.js/archive/${deckjsVersion}.zip!deck.js-${deckjsVersion}",
            deckjsDir, recursive:true, overwrite:true
        }
    }
}

download {
    description "Download extra deckjs resources"
    outputs.dir templateDir
    outputs.dir deckjsDir
}

task overwriteHamlTemplate {
    doLast {
        def documentTemplate = new File(templateDir, 'haml/document.html.haml')
        def template = documentTemplate.text
        
        if (!template.contains('%footer(class="copyright")')) {
            documentTemplate.withPrintWriter { writer ->
                template.eachLine { line ->
                    writer.println line
                    if (line ==~ /\s+\.deck-container/) {
                        writer.println '      %footer(class="copyright")'
                        writer.println '        %img{:src=> image_uri("logo-circle.png"), :width=> "40", :align=> "left"} &copy; 2017 Object Computing Inc.'
                    }
                }
            }
        }
    }   
}
overwriteHamlTemplate.dependsOn download

asciidoctorj {
    version = '1.5.5'
}

asciidoctor {
    dependsOn jrubyPrepare, overwriteHamlTemplate

    sources {
        include 'index.adoc'
    }

    resources {
        from (sourceDir) {
            include 'images/**'
        }
        from ('src/docs') {
            include 'theme/**'
        }
        from (downloadDir) {
            include 'deck.js/**'
        }
    }

    backends 'deckjs'

    attributes \
        'build-gradle': file('build.gradle'),
        'sourcedir': file('src/docs/asciidoc'),
        'endpoint-url': 'http://example.org',
        'source-highlighter': 'coderay',
        'imagesdir': './images',
        'toc': 'left',
        'icons': 'font',
        'setanchors': '',
        'idprefix': '',
        'idseparator': '-',
        'docinfo1': '',
        'customcss': 'theme/fonts.css',
        'status': ''

    options template_dirs : [new File(templateDir,'haml').absolutePath ]

    dependsOn download
}
