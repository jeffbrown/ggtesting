buildscript {
    dependencies {
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0'
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.8.1'
    id 'com.github.jruby-gradle.base' version '1.5.0'
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'org.asciidoctor.convert'

ext {
    deckjsVersion = 'master'
    asciidoctorBackendVersion = 'master'
    downloadDir = new File(buildDir,'download')
    templateDir = new File(downloadDir,'templates')
    deckjsDir   = new File(downloadDir,'deck.js')
}

repositories {
    jcenter()
}

dependencies {
    gems 'rubygems:haml:4.0.7'
}

task download {
    doLast {
        mkdir downloadDir
        vfs {
            cp "zip:https://github.com/asciidoctor/asciidoctor-deck.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-deck.js-${asciidoctorBackendVersion}/templates",
            templateDir, recursive:true, overwrite:true
            cp "zip:https://github.com/imakewebthings/deck.js/archive/${deckjsVersion}.zip!deck.js-${deckjsVersion}",
            deckjsDir, recursive:true, overwrite:true
        }
    }
}

download {
    description "Download extra deckjs resources"
    outputs.dir templateDir
    outputs.dir deckjsDir
}

asciidoctorj {
    version = '1.5.5'
}

asciidoctor {
    dependsOn jrubyPrepare

    sources {
        include 'index.adoc'
    }

    resources {
        from (sourceDir) {
            include 'images/**'
        }
        from (downloadDir) {
            include 'deck.js/**'
        }
    }

    backends 'html5'

    attributes \
        'build-gradle': file('build.gradle'),
        'sourcedir': file('src/docs/asciidoc'),
        'endpoint-url': 'http://example.org',
        'source-highlighter': 'coderay',
        'imagesdir': './images',
        'toc': 'left',
        'icons': 'font',
        'setanchors': '',
        'idprefix': '',
        'idseparator': '-',
        'docinfo1': ''

    options template_dirs : [new File(templateDir,'haml').absolutePath ]

    dependsOn download
}


task copyOciBrandingImages(type: Copy) {
    from "${rootProject.projectDir}/src/main/resources/images"
    into "${rootProject.buildDir}/asciidoc/html5/images"
    include "**/*.png"
}

task ociBranding {
    dependsOn copyOciBrandingImages
    doLast {
        File cssFile = new File("${rootProject.projectDir}/src/main/resources/css/main.css")
        assert cssFile.exists()
        String css = cssFile.text
        File f = new File("${rootProject.buildDir}/asciidoc/html5/index.html")
        assert f.exists()
        f.text = f.text.replaceAll('</head>', "<style text='type/css'>${css}</style></head>".toString())
    }
}

asciidoctor.finalizedBy(ociBranding)