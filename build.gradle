buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {        
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath 'com.bertramlabs.plugins:asset-pipeline-gradle:2.5.0'
        classpath "org.grails.plugins:hibernate:4.3.10.5"

        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:0.4.0'
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta3'
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }

}

plugins {
    id "io.spring.dependency-management" version "0.5.2.RELEASE"
}

version "0.1"
group "testingdemo"

apply plugin: "spring-boot"
apply plugin: "war"
apply plugin: "asset-pipeline"
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "org.grails.grails-web"
apply plugin: "org.grails.grails-gsp"

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'org.asciidoctor.convert'

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

assets {
    minifyJs = true
    minifyCss = true
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

// tag::begin_dependencies[]
dependencies {
// end::begin_dependencies[]
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"

    compile "org.grails.plugins:hibernate"
    compile "org.grails.plugins:cache"
    compile "org.hibernate:hibernate-ehcache"
    compile "org.grails.plugins:scaffolding"

    runtime "org.grails.plugins:asset-pipeline"

    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"

    // Note: It is recommended to update to a more robust driver (Chrome, Firefox etc.)
    testRuntime 'org.seleniumhq.selenium:selenium-htmlunit-driver:2.44.0'

    console "org.grails:grails-console"
    // tag::http_builder[]

    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    // end::http_builder[]
// tag::end_dependencies[]
}
// end::end_dependencies[]

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}


ext {
    revealjsVersion = '2.6.2'
    asciidoctorBackendVersion = 'master'
    downloadDir = new File(buildDir, 'download')
    templateDir = new File(downloadDir, 'templates')
    revealJsDir = new File(downloadDir, 'reveal.js')
}

task download {
    doLast {
        mkdir downloadDir
        mkdir templateDir
        vfs {
            cp "zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
                    templateDir, recursive: true, overwrite: true
            cp "zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
                    revealJsDir, recursive: true, overwrite: true
        }
    }
}
task copyTheme(type:Copy) {
    dependsOn download
    from fileTree("$revealJsDir/reveal.js-${revealjsVersion}")
    into revealJsDir
    from fileTree('src/docs/theme')
    into file("$revealJsDir/css/theme")
}
download {
    description "Download extra deckjs/reveal.js resources"
    inputs.property 'asciidoctorBackendVersion', asciidoctorBackendVersion
    inputs.property 'revealjsVersion', revealjsVersion
    outputs.dir templateDir
    outputs.dir revealJsDir
}
apply from: 'gradle/asciidoctor.gradle'